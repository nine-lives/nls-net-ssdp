import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'checkstyle'

group = 'com.9ls'
version = '0.1.3'

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

repositories {
    mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    checkstyle('com.puppycrawl.tools:checkstyle:6.7')
}

checkstyle {
    configFile = new File(rootDir, "config/checkstyle/checkstyle.xml")
    showViolations = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

boolean release = true
String snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
String stagingUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
//version = version += (release ? '' : '-SNAPSHOT')

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: (release ? stagingUrl : snapshotUrl)) {
              authentication(userName: {sonatypeUsername}, password: {sonatypePassword})
            }

            pom.project {
               name 'nls-net-ssdp'
               packaging 'jar'
               description 'Simple Service Discovery Protocol (SSDP) Java Library'
               url 'https://github.com/mgsmith57/nls-net-ssdp'

               scm {
                   url 'scm:git@github.com:mgsmith57/nls-net-ssdp.git'
                   connection 'scm:git@github.com:mgsmith57/nls-net-ssdp.git'
                   developerConnection 'scm:git@github.com:mgsmith57/nls-net-ssdp.git'
               }

               licenses {
                   license {
                       name 'The MIT License (MIT)'
                       url 'https://raw.githubusercontent.com/mgsmith57/nls-net-ssdp/master/LICENSE'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'mgsmith57'
                       name 'Marc G. Smith'
                   }
               }
           }
        }
    }
}
